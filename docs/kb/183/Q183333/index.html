<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q183333: HOWTO: Create a Dynamically Scrolling Graph | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q183333: HOWTO: Create a Dynamically Scrolling Graph" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/183/Q183333/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/183/Q183333/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/183/Q183333/","headline":"Q183333: HOWTO: Create a Dynamically Scrolling Graph","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q183333-howto-create-a-dynamically-scrolling-graph">Q183333: HOWTO: Create a Dynamically Scrolling Graph</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q183333
Product(s): Microsoft Visual Basic for Windows
Version(s): 
Operating System(s): 
Keyword(s): kbGrpDSVB
Last Modified: 11-JAN-2001

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Learning Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Professional Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Enterprise Edition for Windows, versions 5.0, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

This article shows you how to create a dynamically scrolling graph in Visual
Basic. You can use the information in this article to create a graph as data is
generated, such as in a monitoring program.

MORE INFORMATION
================

The sample project uses Timer and PictureBox controls. The graph is first
created in memory and then copied to the PictureBox control using the BitBlt
function. This method is used to allow the PictureBox control to update properly
when another window partially or completely covers the PictureBox control.

The next section shows you how to create the sample project.

To Create the Sample Project
----------------------------

1. Start a new Standard EXE project in Visual Basic. Form1 is created by
  default.

2. Add a Timer and a PictureBox control to Form1.

3. Copy the following code to the Code window of Form1:

        Option Explicit
        Private Const SRCCOPY = &amp;HCC0020 ' (DWORD) dest = source
        Private Const PS_SOLID = 0

        Private Declare Function CreateCompatibleDC Lib "gdi32" _
                       (ByVal hdc As Long) As Long

        Private Declare Function CreateCompatibleBitmap Lib "gdi32" _
                       (ByVal hdc As Long, _
                       ByVal nWidth As Long, _
                       ByVal nHeight As Long) As Long

        Private Declare Function SelectObject Lib "gdi32" _
                       (ByVal hdc As Long, _
                       ByVal hObject As Long) As Long

        Private Declare Function CreatePen Lib "gdi32" _
                       (ByVal nPenStyle As Long, _
                       ByVal nWidth As Long, _
                       ByVal crColor As Long) As Long

        Private Declare Function LineTo Lib "gdi32" _
                       (ByVal hdc As Long, _
                       ByVal x As Long, _
                       ByVal y As Long) As Long

        Private Declare Function MoveToEx Lib "gdi32" _
                       (ByVal hdc As Long, _
                       ByVal x As Long, _
                       ByVal y As Long, _
                       ByVal lpPoint As Long) As Long

        Private Declare Function BitBlt Lib "gdi32" _
                       (ByVal hDestDC As Long, _
                       ByVal x As Long, _
                       ByVal y As Long, _
                       ByVal nWidth As Long, _
                       ByVal nHeight As Long, _
                       ByVal hSrcDC As Long, _
                       ByVal xSrc As Long, _
                       ByVal ySrc As Long, _
                       ByVal dwRop As Long) As Long

  Private Const pWidth = 250    ' Width of picture box in pixels.
        Private Const pHeight = 150   ' Height of picture box in pixels.
        Private Const pGrid = 25      ' Distance between grid lines.
        Private Const tInterval = 100 ' Interval between timer samplings
                                      ' in milliseconds.
        Private Const pHeightHalf = pHeight \ 2
        Dim counter As Long  ' Number of data points logged so far. Used to
                             ' sync grid.
        Dim oldY As Long     ' Contains the previous y coordinate.
        Dim hDCh As Long, hPenB As Long, hPenC As Long

        Private Sub Form_Load()
            Dim hBmp As Long
            Dim i As Integer
            Me.Show
            Picture1.ScaleMode = 3
            Picture1.Left = 0
            Picture1.Top = 0
            Form1.ScaleMode = 3
            Picture1.Height = 155
            Picture1.Width = 255
            counter = 0
            hDCh = CreateCompatibleDC(Picture1.hdc)
            hBmp = CreateCompatibleBitmap(Picture1.hdc, _
                                         pWidth, _
                                         pHeight)
            Call SelectObject(hDCh, hBmp)
            hPenB = CreatePen(PS_SOLID, 0, vbBlack)
            hPenC = CreatePen(PS_SOLID, 0, vbRed)
            Call SelectObject(hDCh, hPenB)

        ' Plot horizontal grid lines.
            For i = pGrid To pHeight - 1 Step pGrid
                Picture1.Line (0, i)-(pWidth, i)
            Next

        ' Plot vertical grid lines.
            For i = pGrid - (counter Mod pGrid) To _
                             pWidth - 1 Step pGrid
                Picture1.Line (i, 0)-(i, pHeight)
            Next

            Call BitBlt(hDCh, _
                       0, _
                       0, _
                       pWidth, _
                       pHeight, _
                       Picture1.hdc, _
                       0, _
                       0, _
                       SRCCOPY)
            Timer1.Interval = 100
            Timer1.Enabled = True
            oldY = pHeightHalf
        End Sub

        Private Sub Timer1_Timer()
            Dim i As Integer
            Call BitBlt(hDCh, _
                          0, _
                          0, _
                          pWidth - 1, _
                          pHeight, _
                          hDCh, _
                          1, _
                          0, _
                          SRCCOPY)

            If counter Mod pGrid = 0 Then
                Call MoveToEx(hDCh, pWidth - 2, 0, 0)
                Call LineTo(hDCh, pWidth - 2, pHeight)
            End If

            i = Sin(0.1 * counter) * _
                 (pHeightHalf - 1) + _
                 pHeightHalf

            Call SelectObject(hDCh, hPenC)
            Call MoveToEx(hDCh, pWidth - 3, oldY, 0)
            Call LineTo(hDCh, pWidth - 2, i)
            Call SelectObject(hDCh, hPenB)
            Call BitBlt(Picture1.hdc, _
                          0, _
                          0, _
                          pWidth, _
                          pHeight, _
                          hDCh, _
                          0, _
                          0, _
                          SRCCOPY)
            counter = counter + 1
            oldY = i
        End Sub

4. From the Run menu, click Start or press the F5 key to start the program. A
  sine wave in red is displayed in the PictureBox control.

(c) Microsoft Corporation 1998, All Rights Reserved. Contributions by Arsenio
Locsin, Microsoft Corporation


Additional query words: kbVBp kbdsd kbDSupport kbVBp600 kbSDKWin32 kbAPI kbVBp500

======================================================================
Keywords          : kbGrpDSVB 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB500Search kbVB600Search kbVBA500 kbVBA600 kbVB500 kbVB600
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>