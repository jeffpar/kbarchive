<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q193573: HOWTO: Determine the Version of a DLL | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q193573: HOWTO: Determine the Version of a DLL" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/193/Q193573/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/193/Q193573/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/193/Q193573/","headline":"Q193573: HOWTO: Determine the Version of a DLL","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q193573-howto-determine-the-version-of-a-dll">Q193573: HOWTO: Determine the Version of a DLL</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q193573
Product(s): Microsoft FoxPro
Version(s): WINDOWS:3.0,3.0b
Operating System(s): 
Keyword(s): 
Last Modified: 29-JUL-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b 
-------------------------------------------------------------------------------

SUMMARY
=======

It is often necessary to determine the version of a .dll file on a user's
system, such as when using the common controls. Determining the version of the
Comctl32.dll file allows the developer to know, for instance, what functionality
is available at run-time. This article demonstrates how to create and use the
GetDllVersion() function to determine the version of a .dll file.

MORE INFORMATION
================

Visual FoxPro 6.0 offers the _fileversion FoxPro Foundation Class, which
provides this functionality. Alternatively, the AGETFILEVERSION(ArrayName,
cFileName) function can be used in Visual FoxPro 6.0. Visual FoxPro 5.0 can use
the GetFileVersion() function in FoxTools. Users of Visual FoxPro 3.0 can use
the following sample code to obtain the version of a .dll file. Note that this
function only returns versions in a xx.xx.xxxx format. In other words, the file
version in Windows Explorer might return a version of 4.72.2106.4, whereas
GetDllVersion() only returns 4.72.2106.

Save the following program code to a .prg file and run the program:

Sample Code
-----------

     *-- Code begins here.

     *-- Platform IDs for DLLVERSIONINFO.
     *-- The Win32 defines are listed here, but cannot be used
     *-- with Visual FoxPro 3.0 because of the ten character limit.
     #define DLLVER_PLATFORM_WINDOWS         0x00000001      &amp;&amp; Windows 95
     #define DLLVER_PLATFORM_NT              0x00000002      &amp;&amp; Windows NT

     *-- For compatibility with VFP Visual FoxPro 3.0.
     #define WINDOWS         1      &amp;&amp; Windows 95
     #define NT              2      &amp;&amp; Windows NT

     lsReturn = GetDllVersion("comctl32.dll")

     *-- Parse the string.
     liMajorVersion = StrToLong(SUBSTR(lsReturn, 5, 4))
     liMinorVersion = StrToLong(SUBSTR(lsReturn, 9, 4))
     liBuildNumber  = StrToLong(SUBSTR(lsReturn, 13, 4))
     liPlatform     = StrToLong(SUBSTR(lsReturn, 17, 4))

     *-- Determine the platform.
     liOS = StrToLong(SUBSTR(lsReturn, 17, 4))
     IF liOS = WINDOWS
        lsPlatform = "Windows 9x"
     ELSE
        lsPlatform = "Windows NT"
     ENDIF

     lsVersionInfo = "Version: " + ;
       ALLTRIM(STR(liMajorVersion,5,0)) + "." + ;
       ALLTRIM(STR(liMinorVersion,5,0)) + "." + ;
       ALLTRIM(STR(liBuildNumber,5,0)) + CHR(10) + CHR(13) +;
       "Platform: " + lsPlatform

     =MESSAGEBOX(lsVersionInfo)

     *--------------------------------------------------------
     *-- Function:  GetDllVersion()
     *-- Summary:   Returns the version information for a DLL
     *-- Usage:     GetDllVersion(&lt;cDllName&gt;)
     *-- Example:   lsReturn = GetDllVersion("comctl32.dll")
     *--------------------------------------------------------
     FUNCTION GetDllVersion()
     PARAMETERS lsLib

     DECLARE LONG LoadLibrary IN kernel32.DLL STRING @lsLib
     DECLARE LONG GetProcAddress IN kernel32.DLL LONG, STRING lsFunction
     DECLARE LONG FreeLibrary IN kernel32.DLL LONG
     DECLARE LONG DllGetVersion IN (lsLib) STRING @DLLVERSIONINFO

     *-- DLLVERSIONINFO structure is 20 bytes.
     DLLVERSIONINFO = SPACE(20)
     hDll = LoadLibrary(@lsLib)
     IF hDll = 0
        RETURN ""
     ENDIF

     pDllVersion = GetProcAddress(hDll, "DllGetVersion")

     IF pDllVersion = 0
        RETURN ""
     ELSE
        lsSize = LongToStr(20)
        DLLVERSIONINFO = lsSize + SPACE(20 - LEN(lsSize))
        hResult = DllGetVersion(@DLLVERSIONINFO)
     ENDIF
     RETURN DLLVERSIONINFO

     ********************
     FUNCTION LongToStr()
     ********************
     * Passed : 32-bit non-negative numeric value (lnLongval)
     * Returns : ascii character representation of passed value in low-high
     * format (lcRetstr)
     * Example :
     *   m.long = "999999"
     *   m.longstr = long2str(m.long)

     PARAMETERS lnLongval

     PRIVATE i, lcRetstr

     lcRetstr = ""
     FOR i = 24 TO 0 STEP -8
        lcRetstr = CHR(INT(lnLongval/(2^i))) + lcRetstr
        lnLongval = MOD(lnLongval, (2^i))
     NEXT
     RETURN lcRetstr

     ********************
     FUNCTION StrToLong()
     ********************
     * Passed:  4-byte character string (lcLongstr) in low-high ASCII format
     * Returns:  long integer value
     * Example:
     * m.longstr = "1111"
     * m.longval = str2long(m.longstr)

     PARAMETERS lcLongstr

     PRIVATE i, lnRetval

     lnRetval = 0
     FOR i = 0 TO 24 STEP 8
        lnRetval = lnRetval + (ASC(lcLongstr) * (2^i))
        lcLongstr = RIGHT(lcLongstr, LEN(lcLongstr) - 1)
     NEXT

     RETURN lnRetval
     *-- Code ends here.

REFERENCES
==========

(c) Microsoft Corporation 1998. All Rights Reserved. Contributions by Mike
A. Stewart, Microsoft Corporation.


Additional query words: kbVFp300b kbVFp500a kbVFp500 kbAPI

======================================================================
Keywords          :  
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b
Version           : WINDOWS:3.0,3.0b
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>