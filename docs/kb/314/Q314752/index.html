<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=4c5dc89ca309d19d6d9171a268a55a16d7399494">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.3.0 -->
<title>Q314752: FIX: adLockBatchOptimistic Slows Down VFP OLE DB Provider | KnowledgeBase Archive</title>
<meta property="og:title" content="Q314752: FIX: adLockBatchOptimistic Slows Down VFP OLE DB Provider" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/314/Q314752/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/314/Q314752/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"name":null,"description":"An Archive of Early Microsoft KnowledgeBase Articles","author":null,"@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/314/Q314752/","publisher":null,"image":null,"headline":"Q314752: FIX: adLockBatchOptimistic Slows Down VFP OLE DB Provider","dateModified":null,"datePublished":null,"sameAs":null,"mainEntityOfPage":null,"@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q314752-fix-adlockbatchoptimistic-slows-down-vfp-ole-db-provider">Q314752: FIX: adLockBatchOptimistic Slows Down VFP OLE DB Provider</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q314752
Product(s): Microsoft FoxPro
Version(s): 7.0
Operating System(s): 
Keyword(s): kbGrpDSFox kbDSupport kbCodeSnippet kbvfp700 _IK283
Last Modified: 27-FEB-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, version 7.0 
-------------------------------------------------------------------------------

SYMPTOMS
========

If you use the Visual FoxPro 7.0 OLE DB Provider to create an ActiveX Data
Objects (ADO) recordset that uses a lock type of adLockBatchOptimistic, when the
recordset performs a query that returns a single record from a large table, the
query may take a long time to complete. Creating an index on the field in the
WHERE clause of the query can improve performance, but not significantly.

RESOLUTION
==========

To resolve this problem, obtain the latest service pack for Visual FoxPro for
Windows 7.0. For additional information, please see the following article in the
Microsoft Knowledge Base:

  Q316964 How to Obtain the Latest Visual FoxPro for Windows 7.0 Service Pack

STATUS
======

Microsoft has confirmed this to be a problem in Microsoft Visual FoxPro for
Windows 7.0. This problem was first corrected in Visual FoxPro for Windows 7.0
Service Pack 1.

MORE INFORMATION
================

Steps to Reproduce Behavior
---------------------------

To reproduce the behavior, run the following code. The code creates a large
FoxPro table and then queries the table using the Visual FoxPro 7.0 OLE DB
Provider. Although the query returns only a single record, it takes a long time
to complete.

  *!* Comment the 2nd #DEFINE and uncomment the first
  *!* to see increased performance.
  *!* #DEFINE LOC_LockType 1 &amp;&amp; adLockReadOnly
  #DEFINE LOC_LockType  4 &amp;&amp; adLockBatchOptimistic

  LOCAL loConn AS ADODB.CONNECTION, ;
  	loRS AS ADODB.Recordset, ;
  	lnStartTime AS NUMBER, ;
  	lnTotalTime AS NUMBER, ;
  	lnLooper AS INTEGER

  CD JUSTPATH(SYS(16))
  CLOSE DATA ALL
  IF !FILE('GreatBigTable.DBF')
  	WAIT WINDOW "Creating and populating table..." NOCLEAR NOWAIT
  	CREATE TABLE 'GreatBigTable.DBF' (EID I NOT NULL, ;
  		NUM I NOT NULL, ;
  		PYEAR I NOT NULL, ;
  		ESTATU C(2) NOT NULL, ;
  		ECODE I NOT NULL, ;
  		DDATE D NOT NULL, ;
  		DDATET T NOT NULL, ;
  		TYPEC C(1) NOT NULL, ;
  		PLAN I NOT NULL, ;
  		OPTION I NOT NULL, ;
  		EOI L NOT NULL)

  	FOR lnLooper = 1 TO 1000000
  		INSERT INTO GreatBigTable VALUES(lnLooper, ;
  			lnLooper, ;
  			lnLooper, ;
  			"CA", ;
  			lnLooper, ;
  			DATE() + lnLooper, ;
  			DATETIME() + lnLooper, ;
  			"X", ;
  			lnLooper, ;
  			lnLooper, ;
  			.T.)
  	ENDFOR
  	USE IN SELECT("GreatBigTable")
  	WAIT CLEAR
  ENDIF

  lnStartTime = 0
  lnEndTime = 0
  loConn = CREATEOBJECT("ADODB.Connection")
  loRS = CREATEOBJECT("ADODB.RecordSet")

  loConn.OPEN("PROVIDER=vfpoledb.1;DATA SOURCE=" + JUSTPATH(SYS(16)))

  WITH loRS
  	.CursorType = 3 &amp;&amp; adOpenStatic
  	.CursorLocation = 3 &amp;&amp; adUseClient
  	.LockType = LOC_LockType
  	lnStartTime = SECONDS()
  	WAIT WINDOW "Query is running..." NOWAIT NOCLEAR
  	.OPEN("Select * from GreatBigTable where EID= 178441", loConn)
  	lnTotalTime = SECONDS() - lnStartTime

  	WAIT CLEAR
  	MESSAGEBOX(TRANSFORM(.RecordCount) + " record return in " + ;
  		TRANSFORM(lnTotalTime) + " seconds.")
  ENDWITH

  loRS.CLOSE
  loConn.CLOSE
  RELEASE ALL

Additional query words: OLEDB kbVFP700sp1fix

======================================================================
Keywords          : kbGrpDSFox kbDSupport kbCodeSnippet kbvfp700 _IK283 
Technology        : kbVFPsearch kbAudDeveloper kbVFP700
Version           : :7.0
Issue type        : kbbug
Solution Type     : kbfix

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="http://github.com/jeffpar">jeffpar</a><br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>