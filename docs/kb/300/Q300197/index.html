<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q300197: HOW TO: Ping an IP Address with Visual Basic by Using ICMP | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q300197: HOW TO: Ping an IP Address with Visual Basic by Using ICMP" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/300/Q300197/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/300/Q300197/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/300/Q300197/","headline":"Q300197: HOW TO: Ping an IP Address with Visual Basic by Using ICMP","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q300197-how-to-ping-an-ip-address-with-visual-basic-by-using-icmp">Q300197: HOW TO: Ping an IP Address with Visual Basic by Using ICMP</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q300197
Product(s): Microsoft Visual Basic for Windows
Version(s): 6.0
Operating System(s): 
Keyword(s): kbAudDeveloper kbgrpdsvc kbHOWTOmaster
Last Modified: 07-AUG-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Enterprise Edition for Windows, version 6.0 
- Microsoft Visual Basic Professional Edition for Windows, version 6.0 
-------------------------------------------------------------------------------


IN THIS TASK
------------

- SUMMARY

   - Use ICMP to Ping in Visual Basic

- REFERENCES

SUMMARY
=======

This step-by-step article describes how to ping a remote Internet Protocol (IP)
address by using Visual Basic 6.0 and the Icmp.dll file.

Use ICMP to Ping in Visual Basic
--------------------------------

1. Start Microsoft Visual Basic 6.0.

2. In the New Project dialog box, click Standard EXE, and then click Open. The
  Visual Basic Design Editor opens, and Form1 is loaded.

3. To add a new module to this project, right-click Project1.vbp in Project
  Explorer, point to Add, and then click Module.

4. In the Add Module dialog box, click Open. The Module1.bas module is added to
  the project and opens in the Code Editor window.

5. At the top of the Code Editor window of Module1, add the following constants
  and function declaration:

  Option Explicit

  'Icmp constants converted from
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wmisdk/wmi/win32_pingstatus.asp

  Private Const ICMP_SUCCESS As Long = 0
  Private Const ICMP_STATUS_BUFFER_TO_SMALL = 11001                   'Buffer Too Small
  Private Const ICMP_STATUS_DESTINATION_NET_UNREACH = 11002           'Destination Net Unreachable
  Private Const ICMP_STATUS_DESTINATION_HOST_UNREACH = 11003          'Destination Host Unreachable
  Private Const ICMP_STATUS_DESTINATION_PROTOCOL_UNREACH = 11004      'Destination Protocol Unreachable
  Private Const ICMP_STATUS_DESTINATION_PORT_UNREACH = 11005          'Destination Port Unreachable
  Private Const ICMP_STATUS_NO_RESOURCE = 11006                       'No Resources
  Private Const ICMP_STATUS_BAD_OPTION = 11007                        'Bad Option
  Private Const ICMP_STATUS_HARDWARE_ERROR = 11008                    'Hardware Error
  Private Const ICMP_STATUS_LARGE_PACKET = 11009                      'Packet Too Big
  Private Const ICMP_STATUS_REQUEST_TIMED_OUT = 11010                 'Request Timed Out
  Private Const ICMP_STATUS_BAD_REQUEST = 11011                       'Bad Request
  Private Const ICMP_STATUS_BAD_ROUTE = 11012                         'Bad Route
  Private Const ICMP_STATUS_TTL_EXPIRED_TRANSIT = 11013               'TimeToLive Expired Transit
  Private Const ICMP_STATUS_TTL_EXPIRED_REASSEMBLY = 11014            'TimeToLive Expired Reassembly
  Private Const ICMP_STATUS_PARAMETER = 11015                         'Parameter Problem
  Private Const ICMP_STATUS_SOURCE_QUENCH = 11016                     'Source Quench
  Private Const ICMP_STATUS_OPTION_TOO_BIG = 11017                    'Option Too Big
  Private Const ICMP_STATUS_BAD_DESTINATION = 11018                   'Bad Destination
  Private Const ICMP_STATUS_NEGOTIATING_IPSEC = 11032                 'Negotiating IPSEC
  Private Const ICMP_STATUS_GENERAL_FAILURE = 11050                   'General Failure

  Public Const WINSOCK_ERROR = "Windows Sockets not responding correctly."
  Public Const INADDR_NONE As Long = &amp;HFFFFFFFF
  Public Const WSA_SUCCESS = 0
  Public Const WS_VERSION_REQD As Long = &amp;H101

  'Clean up sockets.
  'http://support.microsoft.com/default.aspx?scid=kb;EN-US;q154512

  Private Declare Function WSACleanup Lib "WSOCK32.DLL" () As Long

  'Open the socket connection.
  'http://support.microsoft.com/default.aspx?scid=kb;EN-US;q154512
  Private Declare Function WSAStartup Lib "WSOCK32.DLL" (ByVal wVersionRequired As Long, lpWSADATA As WSADATA) As Long

  'Create a handle on which Internet Control Message Protocol (ICMP) requests can be issued.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcesdkr/htm/_wcesdk_icmpcreatefile.asp
  Private Declare Function IcmpCreateFile Lib "icmp.dll" () As Long

  'Convert a string that contains an (Ipv4) Internet Protocol dotted address into a correct address.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winsock/wsapiref_4esy.asp
  Private Declare Function inet_addr Lib "WSOCK32.DLL" (ByVal cp As String) As Long

  'Close an Internet Control Message Protocol (ICMP) handle that IcmpCreateFile opens.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcesdkr/htm/_wcesdk_icmpclosehandle.asp

  Private Declare Function IcmpCloseHandle Lib "icmp.dll" (ByVal IcmpHandle As Long) As Long

  'Information about the Windows Sockets implementation
  'http://support.microsoft.com/default.aspx?scid=kb;EN-US;q154512
  Private Type WSADATA
     wVersion As Integer
     wHighVersion As Integer
     szDescription(0 To 256) As Byte
     szSystemStatus(0 To 128) As Byte
     iMaxSockets As Long
     iMaxUDPDG As Long
     lpVendorInfo As Long
  End Type

  'Send an Internet Control Message Protocol (ICMP) echo request, and then return one or more replies.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcetcpip/htm/cerefIcmpSendEcho.asp
  Private Declare Function IcmpSendEcho Lib "icmp.dll" _
     (ByVal IcmpHandle As Long, _
      ByVal DestinationAddress As Long, _
      ByVal RequestData As String, _
      ByVal RequestSize As Long, _
      ByVal RequestOptions As Long, _
      ReplyBuffer As ICMP_ECHO_REPLY, _
      ByVal ReplySize As Long, _
      ByVal Timeout As Long) As Long
   
  'This structure describes the options that will be included in the header of an IP packet.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcetcpip/htm/cerefIP_OPTION_INFORMATION.asp
  Private Type IP_OPTION_INFORMATION
     Ttl             As Byte
     Tos             As Byte
     Flags           As Byte
     OptionsSize     As Byte
     OptionsData     As Long
  End Type

  'This structure describes the data that is returned in response to an echo request.
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcesdkr/htm/_wcesdk_icmp_echo_reply.asp
  Public Type ICMP_ECHO_REPLY
     address         As Long
     Status          As Long
     RoundTripTime   As Long
     DataSize        As Long
     Reserved        As Integer
     ptrData                 As Long
     Options        As IP_OPTION_INFORMATION
     Data            As String * 250
  End Type

  '-- Ping a string representation of an IP address.
  ' -- Return a reply.
  ' -- Return long code.
  Public Function ping(sAddress As String, Reply As ICMP_ECHO_REPLY) As Long

  Dim hIcmp As Long
  Dim lAddress As Long
  Dim lTimeOut As Long
  Dim StringToSend As String

  'Short string of data to send
  StringToSend = "hello"

  'ICMP (ping) timeout
  lTimeOut = 1000 'ms

  'Convert string address to a long representation.
  lAddress = inet_addr(sAddress)

  If (lAddress &lt;&gt; -1) And (lAddress &lt;&gt; 0) Then
          
      'Create the handle for ICMP requests.
      hIcmp = IcmpCreateFile()
      
      If hIcmp Then
          'Ping the destination IP address.
          Call IcmpSendEcho(hIcmp, lAddress, StringToSend, Len(StringToSend), 0, Reply, Len(Reply), lTimeOut)

          'Reply status
          ping = Reply.Status
          
          'Close the Icmp handle.
          IcmpCloseHandle hIcmp
      Else
          Debug.Print "failure opening icmp handle."
          ping = -1
      End If
  Else
      ping = -1
  End If

  End Function

  'Clean up the sockets.
  'http://support.microsoft.com/default.aspx?scid=kb;EN-US;q154512
  Public Sub SocketsCleanup()
     
     WSACleanup
      
  End Sub

  'Get the sockets ready.
  'http://support.microsoft.com/default.aspx?scid=kb;EN-US;q154512
  Public Function SocketsInitialize() As Boolean

     Dim WSAD As WSADATA

     SocketsInitialize = WSAStartup(WS_VERSION_REQD, WSAD) = ICMP_SUCCESS

  End Function

  'Convert the ping response to a message that you can read easily from constants.
  'For more information about these constants, visit the following Microsoft Web site: 
  'http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wmisdk/wmi/win32_pingstatus.asp

  Public Function EvaluatePingResponse(PingResponse As Long) As String

    Select Case PingResponse
      
    'Success
    Case ICMP_SUCCESS: EvaluatePingResponse = "Success!"
              
    'Some error occurred
    Case ICMP_STATUS_BUFFER_TO_SMALL:    EvaluatePingResponse = "Buffer Too Small"
    Case ICMP_STATUS_DESTINATION_NET_UNREACH: EvaluatePingResponse = "Destination Net Unreachable"
    Case ICMP_STATUS_DESTINATION_HOST_UNREACH: EvaluatePingResponse = "Destination Host Unreachable"
    Case ICMP_STATUS_DESTINATION_PROTOCOL_UNREACH: EvaluatePingResponse = "Destination Protocol Unreachable"
    Case ICMP_STATUS_DESTINATION_PORT_UNREACH: EvaluatePingResponse = "Destination Port Unreachable"
    Case ICMP_STATUS_NO_RESOURCE: EvaluatePingResponse = "No Resources"
    Case ICMP_STATUS_BAD_OPTION: EvaluatePingResponse = "Bad Option"
    Case ICMP_STATUS_HARDWARE_ERROR: EvaluatePingResponse = "Hardware Error"
    Case ICMP_STATUS_LARGE_PACKET: EvaluatePingResponse = "Packet Too Big"
    Case ICMP_STATUS_REQUEST_TIMED_OUT: EvaluatePingResponse = "Request Timed Out"
    Case ICMP_STATUS_BAD_REQUEST: EvaluatePingResponse = "Bad Request"
    Case ICMP_STATUS_BAD_ROUTE: EvaluatePingResponse = "Bad Route"
    Case ICMP_STATUS_TTL_EXPIRED_TRANSIT: EvaluatePingResponse = "TimeToLive Expired Transit"
    Case ICMP_STATUS_TTL_EXPIRED_REASSEMBLY: EvaluatePingResponse = "TimeToLive Expired Reassembly"
    Case ICMP_STATUS_PARAMETER: EvaluatePingResponse = "Parameter Problem"
    Case ICMP_STATUS_SOURCE_QUENCH: EvaluatePingResponse = "Source Quench"
    Case ICMP_STATUS_OPTION_TOO_BIG: EvaluatePingResponse = "Option Too Big"
    Case ICMP_STATUS_BAD_DESTINATION: EvaluatePingResponse = "Bad Destination"
    Case ICMP_STATUS_NEGOTIATING_IPSEC: EvaluatePingResponse = "Negotiating IPSEC"
    Case ICMP_STATUS_GENERAL_FAILURE: EvaluatePingResponse = "General Failure"
              
    'Unknown error occurred
    Case Else: EvaluatePingResponse = "Unknown Response"
          
    End Select

  End Function

  NOTE: Watch for line wrapping when you copy this code into your project. This
  code provides the functions to access the Icmp.dll file. See the inline
  comments for a description of the constants, the declarations, and the
  function calls.

6. In Project Explorer, right-click Form1, and then click View Object. Form1
  loads in the Designer window.

7. Double-click the Form1 body. The Code Editor window opens with the following
  code:

  Private Sub Form_Load()

  End Sub

8. Replace the code in the Form_Load method with the following code:

  Private Sub Form_Load()

     Dim Reply As ICMP_ECHO_REPLY
     Dim lngSuccess As Long
     Dim strIPAddress As String
     
     'Get the sockets ready.
     If SocketsInitialize() Then
        
      'Address to ping
      strIPAddress = "192.168.1.1"
      
      'Ping the IP that is passing the address and get a reply.
      lngSuccess = ping(strIPAddress, Reply)
        
      'Display the results.
      Debug.Print "Address to Ping: " &amp; strIPAddress
      Debug.Print "Raw ICMP code: " &amp; lngSuccess
      Debug.Print "Ping Response Message : " &amp; EvaluatePingResponse(lngSuccess)
      Debug.Print "Time : " &amp; Reply.RoundTripTime &amp; " ms"
        
      'Clean up the sockets.
      SocketsCleanup
        
     Else
     
     'Winsock error failure, initializing the sockets.
     Debug.Print WINSOCK_ERROR
     
     End If
     
  End Sub

9. Locate the following lines of code:

      'Address to ping
      strIPAddress = "192.168.1.1"

  Change the strIPAddress to an IP address you want to ping.

10. To save the project and the files that are associated with the project,
  click Save Project on the File menu.

11. Notice that the results of the code are written to the Immediate window. To
  verify that the Immediate window is open, click Immediate Window on the View
  menu.

12. To test this code, press F5. The results are displayed in the Immediate
  window. The results are similar to the following results:

  

  Address to Ping: 192.168.1.1
  Raw ICMP code: 0
  Ping Response Message : Success!
  Time : 10 ms

NOTE: To ping a domain name, you must first translate the domain name to an
address. For additional information, click the article number below to view the
article in the Microsoft Knowledge Base:

  Q154512 SAMPLE: Winsock.exe Getting HostAddress Using Windows Sockets

REFERENCES
==========

For additional information, click the article numbers below to view the articles
in the Microsoft Knowledge Base:

  Q160215 HOWTO: Obtain the Host IP Address Using Windows Sockets

  Q154512 SAMPLE: Winsock.exe Getting HostAddress Using Windows Sockets

  Win32_PingStatus
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wmisdk/wmi/win32_pingstatus.asp

  WSACleanup
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winsock/wsapiref_120i.asp

  WSAStartup
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/winsock/wsapiref_1v8y.asp

  IcmpSendEcho
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcetcpip/htm/cerefIcmpSendEcho.asp

  IcmpCreateFile
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcetcpip/htm/cerefIcmpCreateFile.asp

  IcmpCloseHandle
  http://msdn.microsoft.com/library/default.asp?url=/library/en-us/wcetcpip/htm/cerefIcmpCloseHandle.asp

Additional query words:

======================================================================
Keywords          : kbAudDeveloper kbgrpdsvc kbHOWTOmaster 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB600Search kbVB600
Version           : :6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>