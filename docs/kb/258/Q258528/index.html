<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q258528: HOWTO: Read a REG_MULTI_SZ From the Registry | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q258528: HOWTO: Read a REG_MULTI_SZ From the Registry" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/258/Q258528/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/258/Q258528/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/258/Q258528/","headline":"Q258528: HOWTO: Read a REG_MULTI_SZ From the Registry","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q258528-howto-read-a-reg_multi_sz-from-the-registry">Q258528: HOWTO: Read a REG_MULTI_SZ From the Registry</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q258528
Product(s): Microsoft FoxPro
Version(s): 3.0,3.0b,5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbAPI kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport kbCodeSnippe
Last Modified: 11-JUN-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 5.0a, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

This article shows how to read a REG_MULTI_SZ value from the registry.

MORE INFORMATION
================

NOTE: This article requires regedt32.exe, which is only available on Microsoft
Windows NT and Microsoft Windows 2000, to add the test key and value. The code
included runs on Windows 9X with an existing REG_MULTI_SZ value.

IMPORTANT: This article contains information about modifying the registry. Before
you modify the registry, make sure to back it up and make sure that you
understand how to restore the registry if a problem occurs. For information
about how to back up, restore, and edit the registry, click the following
article number to view the article in the Microsoft Knowledge Base:

  Q256986 Description of the Microsoft Windows Registry

WARNING: If you use Registry Editor incorrectly, you may cause serious problems
that may require you to reinstall your operating system. Microsoft cannot
guarantee that you can solve problems that result from using Registry Editor
incorrectly. Use Registry Editor at your own risk.

NOTE: This article uses a registry key that is created just for testing and
should be deleted when you are finished.

1. Start Registry Editor (Regedt32.exe).

2. Locate the following key in the registry:

  HKEY_LOCAL_MACHINE\Software

3. On the Edit menu, click Add Key, and then add the following registry Key:

  Key Name:   VfpRegTest

4. Click on the Key added in step 3. On the Edit menu, click Add Value, and then
  add the following value:

  Value Name: TestREG_MULTI_SZ
  Data Type:  REG_MULTI_SZ
  Value:      Line1
              Line2
              Line3
              Line4   

  Be sure to press the Enter key after each line.

5. Quit Registry Editor.

6. From Visual FoxPro, create a program and enter the following code:

  *Start of Code
  #DEFINE HKEY_LOCAL_MACHINE   -2147483646

  LOCAL nKey, cSubKey, cValue, nStrings, acValueRead, nLoopVar
  nKey = HKEY_LOCAL_MACHINE
  cSubKey = "Software\VfpRegTest"
  cValue = "TestREG_MULTI_SZ"

  nStrings = ReadREG_MULTI_SZ(nKey, cSubKey, cValue, @acValueRead)
  IF (nStrings &gt; 0) THEN
     =MESSAGEBOX("Function Successful.")
     FOR nLoopVar = 1 TO nStrings
        ? acValueRead(nLoopVar)
     ENDFOR	
  ELSE
     =MESSAGEBOX("Function Not Successful.")
  ENDIF

  FUNCTION ReadREG_MULTI_SZ
  * This function reads a REG_MULTI_SZ value from the registry. It will return
  * the number of Elements in acValueRead if successful and store the 
  * strings in acValueRead. If not successful, it will return 0 and 
  * acValueRead will contain 0 elements
     PARAMETERS  nKey, cSubKey, cValue,  acValueRead
     * nKey The root key to open. It can be any of the constants defined below
     *  #DEFINE HKEY_CLASSES_ROOT           -2147483648
     *  #DEFINE HKEY_CURRENT_USER           -2147483647
     *  #DEFINE HKEY_LOCAL_MACHINE          -2147483646
     *  #DEFINE HKEY_USERS                  -2147483645
     * cSubKey The SubKey to open.
     * cValue The value that is going to be read.
     * acValueRead Array of strings read from the registry.

     * Constants that are needed for Registry functions
     #DEFINE REG_MULTI_SZ                 7

     * WIN 32 API functions that are used
     DECLARE Integer RegOpenKey IN Win32API ;
        Integer nHKey, String @cSubKey, Integer @nResult
     DECLARE Integer RegQueryValueEx IN Win32API ;
        Integer nHKey, String lpszValueName, Integer dwReserved,;
        Integer @lpdwType, String @lpbData, Integer @lpcbData
     DECLARE Integer RegCloseKey IN Win32API Integer nHKey

     * Local variables used
     LOCAL nErrCode      &amp;&amp; Error Code returned from Registry functions
     LOCAL nKeyHandle    &amp;&amp; Handle to Key that is opened in the Registry
     LOCAL lpdwValueType &amp;&amp; Type of Value that we are looking for
     LOCAL lpbValue      &amp;&amp; The data stored in the value
     LOCAL lpcbValueSize &amp;&amp; Size of the variable
     LOCAL lpdwReserved  &amp;&amp; Reserved Must be 0
     LOCAL lNotDone      &amp;&amp; Used to Exit Loop
     LOCAL nOccurance    &amp;&amp; The occurrence of CHR(0) in the string
     LOCAL nPrevPos      &amp;&amp; Previous element in Array 
     LOCAL nCurrPos      &amp;&amp; Current element in Array being processed
     LOCAL nElements     &amp;&amp; Number of Elements in the Array
     
     * Initialize the variables
     nKeyHandle = 0
     lpdwReserved = 0           
     lpdwValueType = REG_MULTI_SZ
     lNotDone = .T.
     nOccurance = 1
     nPrevPos = 1
     nElements = 0

     nErrCode = RegOpenKey(nKey, cSubKey, @nKeyHandle)
     * If the error code isn't 0, then the key doesn't exist or can't be opened.
     IF (nErrCode # 0) THEN
        RETURN 0
     ENDIF

     lpbValue = ""
     lpcbValueSize = 1 
     * Get the size of the data in the value
     nErrCode=RegQueryValueEx(nKeyHandle, cValue, lpdwReserved, @lpdwValueType, @lpbValue, @lpcbValueSize)

     * Make the buffer big enough
     lpbValue = SPACE(lpcbValueSize)   
     nErrCode=RegQueryValueEx(nKeyHandle, cValue, lpdwReserved, @lpdwValueType, @lpbValue, @lpcbValueSize)
     
     =RegCloseKey(nKeyHandle)
     IF (nErrCode # 0) THEN
        RETURN 0
     ENDIF

     * This loop fills an array with the strings stored in the registry
     DO WHILE lNotDone
        nCurrPos = AT(CHR(0), lpbValue, nOccurance)  &amp;&amp; CHR(0) is used to separate lines in the registry
        IF ((nCurrPos &gt; 0) AND (nCurrPos &lt; lpcbValueSize)) THEN
           nElements = nElements + 1
           DIMENSION acValueRead(nElements)
           acValueRead(nElements) = SUBSTR(lpbValue, nPrevPos, nCurrPos - nPrevPos)
           nPrevPos = nCurrPos + 1
           nOccurance = nOccurance + 1
        ELSE
           lNotDone = .F.
        ENDIF
     ENDDO
  RETURN nElements
  * End of Code

7. Run the program created in step 6. A message box appears saying "Function
  Successful" and the values appear on the desktop.

8. You can replace nKey, cSubKey, and cValue with your information to be read
  from the registry.

(c) Microsoft Corporation 2000, All Rights Reserved. Contributions by Mark
Barnard, Microsoft Corporation.


REFERENCES
==========

For additional information obtaining values from the registry, click the article
number below to view the article in the Microsoft Knowledge Base:

  Q244675 HOWTO:Use Windows Script Host to Read, Write, Delete Registry

Additional query words:

======================================================================
Keywords          : kbAPI kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport kbCodeSnippet 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b kbVFP500 kbVFP600 kbVFP500a
Version           : :3.0,3.0b,5.0,5.0a,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>