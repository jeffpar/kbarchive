<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q258559: HOWTO: Write a REG_SZ Value to the Registry | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q258559: HOWTO: Write a REG_SZ Value to the Registry" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/258/Q258559/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/258/Q258559/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/258/Q258559/","headline":"Q258559: HOWTO: Write a REG_SZ Value to the Registry","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q258559-howto-write-a-reg_sz-value-to-the-registry">Q258559: HOWTO: Write a REG_SZ Value to the Registry</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q258559
Product(s): Microsoft FoxPro
Version(s): 3.0,3.0b,5.0,5.0a,6.0
Operating System(s): 
Keyword(s): kbAPI kbRegistry kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport k
Last Modified: 11-JUN-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual FoxPro for Windows, versions 3.0, 3.0b, 5.0, 5.0a, 6.0 
-------------------------------------------------------------------------------

SUMMARY
=======

This article shows how to write a REG_SZ value to the registry.

MORE INFORMATION
================

IMPORTANT: This article contains information about modifying the registry.
Before you modify the registry, make sure to back it up and make sure that you
understand how to restore the registry if a problem occurs. For information
about how to back up, restore, and edit the registry, click the following
article number to view the article in the Microsoft Knowledge Base:

  Q256986 Description of the Microsoft Windows Registry

WARNING: If you use Registry Editor incorrectly, you may cause serious problems
that may require you to reinstall your operating system. Microsoft cannot
guarantee that you can solve problems that result from using Registry Editor
incorrectly. Use Registry Editor at your own risk.

This information about the Registry Editor in this article is for Microsoft
Windows NT and Microsoft Windows 2000. If you are running Microsoft Windows 95
or Microsoft Windows 98, you need to use the Regedit.exe file instead of the
Regedt32.exe file.

NOTE: This article uses a registry key that is created just for testing and
should be deleted when you are finished.

1. Start Registry Editor (Regedt32.exe).

2. Locate the following key in the registry:

  HKEY_LOCAL_MACHINE\Software

3. On the Edit menu, click Add Key, and then add the following registry key:

  Key Name: VfpRegTest

4. Click on the registry key added in step 3. On the Edit menu, click Add Value,
  and then add the following value:

  Value Name: TestREG_SZ Data Type: REG_SZ Value: None

5. Quit Registry Editor.

6. From Visual FoxPro, create a program and enter the following code:

  *Start of Code
  #DEFINE HKEY_LOCAL_MACHINE          -2147483646

  LOCAL nKey, cSubKey, cValue, lSuccess, cValueToWrite
  nKey = HKEY_LOCAL_MACHINE
  cSubKey = "Software\VfpRegTest"
  cValue = "TestREG_SZ"
  cValueToWrite = "This is a String."

  lSuccess = WriteREG_SZ(nKey, cSubKey, cValue, cValueToWrite)
  IF (lSuccess) THEN
     =MESSAGEBOX("Function Successful.")
  ELSE
     =MESSAGEBOX("Function Not Successful.")
  ENDIF

  FUNCTION WriteREG_SZ
  * This function writes a REG_SZ value to the registry. If successful,
  * its will return .T.. If not successful, it will return .F..
     PARAMETERS  nKey, cSubKey, cValue, cValueToWrite
     * nKey The root key to open. It can be any of the constants defined below
     *#DEFINE HKEY_CLASSES_ROOT           -2147483648
     *#DEFINE HKEY_CURRENT_USER           -2147483647
     *#DEFINE HKEY_LOCAL_MACHINE          -2147483646
     *#DEFINE HKEY_USERS                  -2147483645
     * cSubKey The SubKey to open.
     * cValueToWrite The value being written to the registry.

     * Constants that are needed for Registry functions
     #DEFINE REG_SZ   1

     * WIN 32 API functions that are used
     DECLARE Integer RegOpenKey IN Win32API ;
        Integer nHKey, String @cSubKey, Integer @nResult
     DECLARE Integer RegSetValueEx IN Win32API ;
        Integer hKey, String lpszValueName, Integer dwReserved,;
        Integer fdwType, String lpbData, Integer cbData
     DECLARE Integer RegCloseKey IN Win32API Integer nHKey

     * Local variables used
     LOCAL nErrCode      &amp;&amp; Error Code returned from Registry functions
     LOCAL nKeyHandle    &amp;&amp; Handle to Key that is opened in the Registry
     LOCAL lpdwValueType &amp;&amp; Type of Value that we are looking for
     LOCAL lpbValue      &amp;&amp; The data stored in the value
     LOCAL lpcbValueSize &amp;&amp; Size of the variable
     LOCAL lpdwReserved  &amp;&amp; Reserved Must be 0
      
     * Initialize the variables
     nKeyHandle = 0
     lpdwReserved = 0           
     lpdwValueType = REG_SZ
     lpbValue = cValueToWrite
     
     nErrCode = RegOpenKey(nKey, cSubKey, @nKeyHandle)
     * If the error code isn't 0, then the key doesn't exist or can't be opened.
     IF (nErrCode # 0) THEN
        RETURN .F.
     ENDIF

     lpcbValueSize = LEN(lpbValue)   &amp;&amp; Store the length of the string
     nErrCode=RegSetValueEx(nKeyHandle, cValue, lpdwReserved, lpdwValueType, lpbValue, lpcbValueSize)
     
     =RegCloseKey(nKeyHandle)
     IF (nErrCode # 0) THEN
        RETURN .F.
     ENDIF
  RETURN .T.
  * End of Code     

7. Run the program created in step 6. A message box appears indicating "Function
  Successful."

8. You can replace nKey, cSubKey, cValue, and cValueToWrite with your
  information to be written to the registry.

(c) Microsoft Corporation 2000, All Rights Reserved. Contributions by Mark
Barnard, Microsoft Corporation.


REFERENCES
==========

For additional information obtaining values from the registry, click the article
numbers below to view the articles in the Microsoft Knowledge Base:

  Q244675 HOWTO:Use Windows Script Host to Read, Write, Delete Registry

  Q155267 Registry Editors Installed by Windows NT 4.0

Additional query words:

======================================================================
Keywords          : kbAPI kbRegistry kbvfp300 kbvfp300b kbvfp500 kbvfp500a kbvfp600 kbGrpDSFox kbDSupport kbCodeSnippet 
Technology        : kbVFPsearch kbAudDeveloper kbVFP300 kbVFP300b kbVFP500 kbVFP600 kbVFP500a
Version           : :3.0,3.0b,5.0,5.0a,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>