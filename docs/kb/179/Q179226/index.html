<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=231b77c990cb1dad78d33119176ff1f53331eb63">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.4.0 -->
<title>Q179226: PRB: RDO: CommitTrans/RollBackTrans Closes Cursor Causing Error | KnowledgeBase Archive</title>
<meta name="generator" content="Jekyll v3.7.3" />
<meta property="og:title" content="Q179226: PRB: RDO: CommitTrans/RollBackTrans Closes Cursor Causing Error" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/179/Q179226/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/179/Q179226/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"description":"An Archive of Early Microsoft KnowledgeBase Articles","@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/179/Q179226/","headline":"Q179226: PRB: RDO: CommitTrans/RollBackTrans Closes Cursor Causing Error","@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q179226-prb-rdo-committransrollbacktrans-closes-cursor-causing-error">Q179226: PRB: RDO: CommitTrans/RollBackTrans Closes Cursor Causing Error</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q179226
Product(s): Microsoft Visual Basic for Windows
Version(s): 
Operating System(s): 
Keyword(s): 
Last Modified: 09-JUN-1999

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Enterprise Edition, 32-bit, for Windows, version 4.0 
-------------------------------------------------------------------------------

SYMPTOMS
========

Attempting to perform any action on an rdoResultset object connected to a SQL
Server database after committing or rolling back a transaction on the
rdoResultset's rdoConnection object (or simply the act of committing or rolling
back the transaction), will result in the following error:

  40002
  S1010:[Microsoft][ODBC SQL Server Driver]Function sequence error

Similar errors can be expected with other servers.

CAUSE
=====

Depending on the cursor type, the error above will be raised either on the
CommitTrans or RollBackTrans or when any attempt is made to access a property or
method of the rdoResultset object after the CommitTrans or RollBackTrans.

This occurs because many servers, including SQL Server, default to closing or
deleting cursors at the end of the transaction. Any action on the rdoResultset
causes an action on the now-closed cursor, which causes the error. When using
server-side keyset or dynamic cursors with SQL Server, RDO will erroneously call
SQLExtendedFetch on the CommitTrans or RollBackTrans statement, raising this
error.

RESOLUTION
==========

While this error can be trapped and handled, the better solution is to change
the connection option so that the cursors are not closed at the end of the
transaction. This behavior can be changed for most servers. The code to do this
with SQL Server follows. For other servers, please consult the ODBC driver and
server documentation for the equivalent call.

The following code works around the problem:

1. Start a New Project.

2. Add a command button to the default form.

3. Add the following to the General Declaration section:

        Option Explicit
        'SQL Server specific connection options
         Private Const SQL_PRESERVE_CURSORS As Long = 1204
         Private Const SQL_PC_ON As Long = 1
         Private Const SQL_PC_OFF As Long = 0

         'Possible ODBC function returns
         Private Const SQL_ERROR As Integer = -1
         Private Const SQL_INVALID_HANDLE As Integer = -2
         Private Const SQL_NO_DATA_FOUND As Integer = 100
         Private Const SQL_SUCCESS As Integer = 0
         Private Const SQL_SUCCESS_WITH_INFO As Integer = 1

         Private Declare Function SQLSetConnectOption Lib "odbc32.dll" _
            (ByVal hdbc As Long, ByVal fOption As Integer, pvParam As Any) _
                As Integer

         Private Declare Function SQLGetConnectOption Lib "odbc32.dll" _
            (ByVal hdbc As Long, ByVal fOption As Integer, pvParam As Any) _
                  As Integer

4. Add the following code to the click event of the CommandButton: (The server
  will need to be changed.)

        Private Sub Command1_Click()
         Dim intRet As Integer
         Dim lngConnOption As Long
         Dim Conn As rdoConnection
         Dim Rslt As rdoResultset

         'Make DSN-less connection. CHANGE SERVER UID PWD for your server
          Set Conn = rdoEnvironments(0).OpenConnection(_
              "", rdDriverComplete, False, _
         "DRIVER={SQL Server};SERVER=hoohaa;DSN=;DATABASE=pubs;UID=sa;PWD=;")
         'Getting connection option
          intRet = SQLGetConnectOption(Conn.hdbc, SQL_PRESERVE_CURSORS, _
             lngConnOption)
          If SQL_SUCCESS &lt;&gt; intRet Then
            MsgBox "SQLGetConnectOption Failed", "ERROR", vbCritical
            Conn.Close
           Exit Sub
         End If
         'display it
         Select Case lngConnOption
          Case SQL_PC_OFF
             MsgBox "Cursor Behavior: Close", , "Connection Option Value"
          Case SQL_PC_ON
             MsgBox "Cursor Behavior: Maintain", , "Connection Option Value"
          Case Else
            MsgBox "ERROR: Unknown Connection Option", , _
             "Connection Option Value", vbCritical
         End Select
         'uncomment the next 2 lines to stop error
       'intRet = SQLSetConnectOption(Conn.hdbc, SQL_PRESERVE_CURSORS, ByVal _
       'SQL_PC_ON)
        If intRet &lt;&gt; SQL_SUCCESS Then
         MsgBox "SQLSetConnectOption Failed", , "ERROR", vbCritical
         Conn.Close
         Exit Sub
        End If
        MsgBox "Connection Option Set to SQL_PC_ON", , _
           "Connection Option Status"
        Set Rslt = Conn.OpenResultset("select * from authors", _
          rdOpenDynamic, rdConcurValues)
        Conn.BeginTrans
        Rslt.MoveFirst
        Rslt.Edit
        Rslt.rdoColumns("address").Value = "test"
        Rslt.Update
        'will error on this line if connection option not set
        Conn.RollbackTrans
        Rslt.MoveLast
        Rslt.Close
        Conn.Close
       End Sub

5. Run the project and note that the code fails as indicated.

6. Uncomment the noted lines. Run the project again and note that the code does
  not fail.

STATUS
======

This behavior is not by design, but since a workaround exists, it will not be
fixed.

REFERENCES
==========

ODBC 2.0 Programmer's Guide and SDK Reference

(c) Microsoft Corporation 1998. All Rights Reserved.
Contributions by Troy Cambra, Microsoft Corporation


======================================================================
Keywords          :  
Technology        : kbVBSearch kbAudDeveloper kbVB400Search kbVB400
Issue type        : kbprb

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="https://www.pcjs.org">PCjs</a>.<br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>