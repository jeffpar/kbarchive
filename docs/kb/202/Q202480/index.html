<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=4c5dc89ca309d19d6d9171a268a55a16d7399494">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.3.0 -->
<title>Q202480: HOW TO: Determine Printer Status and Print Job Status from VB | KnowledgeBase Archive</title>
<meta property="og:title" content="Q202480: HOW TO: Determine Printer Status and Print Job Status from VB" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/202/Q202480/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/202/Q202480/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"name":null,"description":"An Archive of Early Microsoft KnowledgeBase Articles","author":null,"@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/202/Q202480/","publisher":null,"image":null,"headline":"Q202480: HOW TO: Determine Printer Status and Print Job Status from VB","dateModified":null,"datePublished":null,"sameAs":null,"mainEntityOfPage":null,"@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q202480-how-to-determine-printer-status-and-print-job-status-from-vb">Q202480: HOW TO: Determine Printer Status and Print Job Status from VB</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q202480
Product(s): Microsoft Visual Basic for Windows
Version(s): 4.0,5.0,6.0
Operating System(s): 
Keyword(s): kbAPI kbPrinting kbSpooler KbVBA kbVBp kbVBp400 kbVBp500 kbVBp600 kbGrpDSVB kbDSupport
Last Modified: 20-MAY-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Enterprise Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Professional Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Standard Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Enterprise Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Professional Edition, 32-bit, for Windows, version 4.0 
-------------------------------------------------------------------------------


IN THIS TASK
------------

- SUMMARY

   - Description of the Technique
- Step-by-Step Sample
- Troubleshooting

- REFERENCES

SUMMARY
=======

This article describes how to use Windows application programming interface
(API) functions in Visual Basic to determine the printer status or the print job
status programmatically. Although an application does not typically have to
check the status of a printer before printing, it may be useful to determine the
status of a printer or print job programmatically.

Description of the Technique
----------------------------

The term "printer" can refer to a hardware device, a queue, a driver, or even a
port. In this article, the term "printer" is defined as a local print queue. The
code sample in this article returns the statuses that the operating system
reports. This is the same status that the spooler reports, which you can check
by watching the local print queue.

For example, to view the queue on a computer that is running Microsoft Windows
98, click Start, point to Settings, click Printers, and then double-click the
icon for the printer whose queue you want to view.

NOTE: You cannot communicate directly with the physical printer. You should not
have to do this because the operating system must arbitrate access to the
hardware.

The sample code in this article examines the local print queue, which obtains
information from the port monitor, which in turn communicates with the physical
device. For a more detailed description of how this works, see the articles that
are listed in the "References" section of this article.

This sample reports the status from the printer and from the jobs, but note that
the job status information is generally more reliable for applications. Ideally,
you should examine the jobs and the printer statuses separately, and the code
should infer the "meta" status of the queue. However, for most uses, your code
can rely on the job statuses. Also, this example loops through the jobs and
checks and reports the status of each job. This is because your job may follow
another job that is reporting a problem, such as being out of paper or getting
jammed.

NOTE: The system only checks the status when the system has a job to spool.
Otherwise, the queue is considered "ready" because the queue can accept jobs,
even if the hardware is in an error state. For example, if the last job that was
printed used the last piece of paper, the operating system does not know this
until the system tries to print again.

Additionally, although there are many statuses that may be reported, many are not
supported in practice. The printer hardware and the port monitor determine which
statuses to report. For example, if the printer is out of paper and offline, the
status may be reported as "Printing" because that is what the job is trying to
do. Therefore, a queue that displays "Ready" does not guarantee that your print
job will complete successfully.

This code sample examines only the local queue, which should suffice for most
applications. However, connecting to remote printers can become fairly complex.
You can have chained queues, in which the port for the local queue is actually
another queue. You can have printer pooling, in which multiple printers work
from a common super queue. When the architecture becomes more complex, the code
to retrieve a meaningful status also becomes more complex, and the usefulness of
the status is reduced.

On Microsoft Windows 95, Microsoft Windows 98, and Microsoft Windows Millennium
Edition only, you can also examine the PrinterInfo.Attributes field for the bit
PRINTER_ATTRIBUTE_WORK_OFFLINE. This state is something that typically occurs
because of user action (for example, if the user right-clicks a printer icon and
then clicks Work Offline). This state does not occur because of the state change
of a print job, although the operating system can force state if the printer
cannot be connected to despool a job. In that case, the status is typically
reported as USER_INERVENTION_REQUIRED.

Step-by-Step Example
--------------------

1. Create a new Standard EXE project in Visual Basic. By default, Form1 is
  created.

2. On the Project menu, click Add Module, and then add the following code:

  Option Explicit

  Public Declare Function lstrcpy Lib "kernel32" _
     Alias "lstrcpyA" _
     (ByVal lpString1 As String, _
     ByVal lpString2 As String) _
     As Long

  Public Declare Function OpenPrinter Lib "winspool.drv" _
     Alias "OpenPrinterA" _
     (ByVal pPrinterName As String, _
     phPrinter As Long, _
     pDefault As PRINTER_DEFAULTS) _
     As Long

  Public Declare Function GetPrinter Lib "winspool.drv" Alias "GetPrinterA" _
     (ByVal hPrinter As Long, _
     ByVal Level As Long, _
     pPrinter As Byte, _
     ByVal cbBuf As Long, _
     pcbNeeded As Long) _
     As Long

  Public Declare Function ClosePrinter Lib "winspool.drv" _
     (ByVal hPrinter As Long) _
     As Long

  Public Declare Sub CopyMemory Lib "kernel32" Alias "RtlMoveMemory" _
     (Destination As Any, _
     Source As Any, _
     ByVal Length As Long)

  Public Declare Function EnumJobs Lib "winspool.drv" Alias "EnumJobsA" _
     (ByVal hPrinter As Long, _
     ByVal FirstJob As Long, _
     ByVal NoJobs As Long, _
     ByVal Level As Long, _
     pJob As Byte, _
     ByVal cdBuf As Long, _
     pcbNeeded As Long, _
     pcReturned As Long) _
     As Long
     
  ' constants for PRINTER_DEFAULTS structure
  Public Const PRINTER_ACCESS_USE = &amp;H8
  Public Const PRINTER_ACCESS_ADMINISTER = &amp;H4

  ' constants for DEVMODE structure
  Public Const CCHDEVICENAME = 32
  Public Const CCHFORMNAME = 32

  Public Type PRINTER_DEFAULTS
     pDatatype As String
     pDevMode As Long
     DesiredAccess As Long
  End Type

  Public Type DEVMODE
     dmDeviceName As String * CCHDEVICENAME
     dmSpecVersion As Integer
     dmDriverVersion As Integer
     dmSize As Integer
     dmDriverExtra As Integer
     dmFields As Long
     dmOrientation As Integer
     dmPaperSize As Integer
     dmPaperLength As Integer
     dmPaperWidth As Integer
     dmScale As Integer
     dmCopies As Integer
     dmDefaultSource As Integer
     dmPrintQuality As Integer
     dmColor As Integer
     dmDuplex As Integer
     dmYResolution As Integer
     dmTTOption As Integer
     dmCollate As Integer
     dmFormName As String * CCHFORMNAME
     dmLogPixels As Integer
     dmBitsPerPel As Long
     dmPelsWidth As Long
     dmPelsHeight As Long
     dmDisplayFlags As Long
     dmDisplayFrequency As Long
  End Type

  Type SYSTEMTIME
     wYear As Integer
     wMonth As Integer
     wDayOfWeek As Integer
     wDay As Integer
     wHour As Integer
     wMinute As Integer
     wSecond As Integer
     wMilliseconds As Integer
  End Type

  Type JOB_INFO_2
     JobId As Long
     pPrinterName As Long
     pMachineName As Long
     pUserName As Long
     pDocument As Long
     pNotifyName As Long
     pDatatype As Long
     pPrintProcessor As Long
     pParameters As Long
     pDriverName As Long
     pDevMode As Long
     pStatus As Long
     pSecurityDescriptor As Long
     Status As Long
     Priority As Long
     Position As Long
     StartTime As Long
     UntilTime As Long
     TotalPages As Long
     Size As Long
     Submitted As SYSTEMTIME
     time As Long
     PagesPrinted As Long
  End Type

  Type PRINTER_INFO_2
     pServerName As Long
     pPrinterName As Long
     pShareName As Long
     pPortName As Long
     pDriverName As Long
     pComment As Long
     pLocation As Long
     pDevMode As Long
     pSepFile As Long
     pPrintProcessor As Long
     pDatatype As Long
     pParameters As Long
     pSecurityDescriptor As Long
     Attributes As Long
     Priority As Long
     DefaultPriority As Long
     StartTime As Long
     UntilTime As Long
     Status As Long
     cJobs As Long
     AveragePPM As Long
  End Type

  Public Const ERROR_INSUFFICIENT_BUFFER = 122
  Public Const PRINTER_STATUS_BUSY = &amp;H200
  Public Const PRINTER_STATUS_DOOR_OPEN = &amp;H400000
  Public Const PRINTER_STATUS_ERROR = &amp;H2
  Public Const PRINTER_STATUS_INITIALIZING = &amp;H8000
  Public Const PRINTER_STATUS_IO_ACTIVE = &amp;H100
  Public Const PRINTER_STATUS_MANUAL_FEED = &amp;H20
  Public Const PRINTER_STATUS_NO_TONER = &amp;H40000
  Public Const PRINTER_STATUS_NOT_AVAILABLE = &amp;H1000
  Public Const PRINTER_STATUS_OFFLINE = &amp;H80
  Public Const PRINTER_STATUS_OUT_OF_MEMORY = &amp;H200000
  Public Const PRINTER_STATUS_OUTPUT_BIN_FULL = &amp;H800
  Public Const PRINTER_STATUS_PAGE_PUNT = &amp;H80000
  Public Const PRINTER_STATUS_PAPER_JAM = &amp;H8
  Public Const PRINTER_STATUS_PAPER_OUT = &amp;H10
  Public Const PRINTER_STATUS_PAPER_PROBLEM = &amp;H40
  Public Const PRINTER_STATUS_PAUSED = &amp;H1
  Public Const PRINTER_STATUS_PENDING_DELETION = &amp;H4
  Public Const PRINTER_STATUS_PRINTING = &amp;H400
  Public Const PRINTER_STATUS_PROCESSING = &amp;H4000
  Public Const PRINTER_STATUS_TONER_LOW = &amp;H20000
  Public Const PRINTER_STATUS_USER_INTERVENTION = &amp;H100000
  Public Const PRINTER_STATUS_WAITING = &amp;H2000
  Public Const PRINTER_STATUS_WARMING_UP = &amp;H10000
  Public Const JOB_STATUS_PAUSED = &amp;H1
  Public Const JOB_STATUS_ERROR = &amp;H2
  Public Const JOB_STATUS_DELETING = &amp;H4
  Public Const JOB_STATUS_SPOOLING = &amp;H8
  Public Const JOB_STATUS_PRINTING = &amp;H10
  Public Const JOB_STATUS_OFFLINE = &amp;H20
  Public Const JOB_STATUS_PAPEROUT = &amp;H40
  Public Const JOB_STATUS_PRINTED = &amp;H80
  Public Const JOB_STATUS_DELETED = &amp;H100
  Public Const JOB_STATUS_BLOCKED_DEVQ = &amp;H200
  Public Const JOB_STATUS_USER_INTERVENTION = &amp;H400
  Public Const JOB_STATUS_RESTART = &amp;H800

  Public Function GetString(ByVal PtrStr As Long) As String
     Dim StrBuff As String * 256
     
     'Check for zero address
     If PtrStr = 0 Then
        GetString = " "
        Exit Function
     End If
     
     'Copy data from PtrStr to buffer.
     CopyMemory ByVal StrBuff, ByVal PtrStr, 256
     
     'Strip any trailing nulls from string.
     GetString = StripNulls(StrBuff)
  End Function

  Public Function StripNulls(OriginalStr As String) As String
     'Strip any trailing nulls from input string.
     If (InStr(OriginalStr, Chr(0)) &gt; 0) Then
        OriginalStr = Left(OriginalStr, InStr(OriginalStr, Chr(0)) - 1)
     End If

     'Return modified string.
     StripNulls = OriginalStr
  End Function

  Public Function PtrCtoVbString(Add As Long) As String
      Dim sTemp As String * 512
      Dim x As Long

      x = lstrcpy(sTemp, Add)
      If (InStr(1, sTemp, Chr(0)) = 0) Then
           PtrCtoVbString = ""
      Else
           PtrCtoVbString = Left(sTemp, InStr(1, sTemp, Chr(0)) - 1)
      End If
  End Function

  Public Function CheckPrinterStatus(PI2Status As Long) As String
     Dim tempStr As String
     
     If PI2Status = 0 Then   ' Return "Ready"
        CheckPrinterStatus = "Printer Status = Ready" &amp; vbCrLf
     Else
        tempStr = ""   ' Clear
        If (PI2Status And PRINTER_STATUS_BUSY) Then
           tempStr = tempStr &amp; "Busy  "
        End If
        
        If (PI2Status And PRINTER_STATUS_DOOR_OPEN) Then
           tempStr = tempStr &amp; "Printer Door Open  "
        End If
        
        If (PI2Status And PRINTER_STATUS_ERROR) Then
           tempStr = tempStr &amp; "Printer Error  "
        End If
        
        If (PI2Status And PRINTER_STATUS_INITIALIZING) Then
           tempStr = tempStr &amp; "Initializing  "
        End If
        
        If (PI2Status And PRINTER_STATUS_IO_ACTIVE) Then
           tempStr = tempStr &amp; "I/O Active  "
        End If

        If (PI2Status And PRINTER_STATUS_MANUAL_FEED) Then
           tempStr = tempStr &amp; "Manual Feed  "
        End If
        
        If (PI2Status And PRINTER_STATUS_NO_TONER) Then
           tempStr = tempStr &amp; "No Toner  "
        End If
        
        If (PI2Status And PRINTER_STATUS_NOT_AVAILABLE) Then
           tempStr = tempStr &amp; "Not Available  "
        End If
        
        If (PI2Status And PRINTER_STATUS_OFFLINE) Then
           tempStr = tempStr &amp; "Off Line  "
        End If
        
        If (PI2Status And PRINTER_STATUS_OUT_OF_MEMORY) Then
           tempStr = tempStr &amp; "Out of Memory  "
        End If
        
        If (PI2Status And PRINTER_STATUS_OUTPUT_BIN_FULL) Then
           tempStr = tempStr &amp; "Output Bin Full  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PAGE_PUNT) Then
           tempStr = tempStr &amp; "Page Punt  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PAPER_JAM) Then
           tempStr = tempStr &amp; "Paper Jam  "
        End If

        If (PI2Status And PRINTER_STATUS_PAPER_OUT) Then
           tempStr = tempStr &amp; "Paper Out  "
        End If
        
        If (PI2Status And PRINTER_STATUS_OUTPUT_BIN_FULL) Then
           tempStr = tempStr &amp; "Output Bin Full  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PAPER_PROBLEM) Then
           tempStr = tempStr &amp; "Page Problem  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PAUSED) Then
           tempStr = tempStr &amp; "Paused  "
        End If

        If (PI2Status And PRINTER_STATUS_PENDING_DELETION) Then
           tempStr = tempStr &amp; "Pending Deletion  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PRINTING) Then
           tempStr = tempStr &amp; "Printing  "
        End If
        
        If (PI2Status And PRINTER_STATUS_PROCESSING) Then
           tempStr = tempStr &amp; "Processing  "
        End If
        
        If (PI2Status And PRINTER_STATUS_TONER_LOW) Then
           tempStr = tempStr &amp; "Toner Low  "
        End If

        If (PI2Status And PRINTER_STATUS_USER_INTERVENTION) Then
           tempStr = tempStr &amp; "User Intervention  "
        End If
        
        If (PI2Status And PRINTER_STATUS_WAITING) Then
           tempStr = tempStr &amp; "Waiting  "
        End If
        
        If (PI2Status And PRINTER_STATUS_WARMING_UP) Then
           tempStr = tempStr &amp; "Warming Up  "
        End If
        
        'Did you find a known status?
        If Len(tempStr) = 0 Then
           tempStr = "Unknown Status of " &amp; PI2Status
        End If
        
        'Return the Status
        CheckPrinterStatus = "Printer Status = " &amp; tempStr &amp; vbCrLf
     End If
  End Function

3. Add three CommandButton controls.

4. Add three TextBox controls to Form1, and then configure the TextBox controls
  as follows:

  a. Set the MultiLine property of each TextBox control to True.

  b. Size each TextBox to approximately five inches wide and three inches long.

  c. Set the ScrollBars property of each TextBox control to "2 - Vertical".

5. Add a Timer control to Form1.

6. Add the following code to the Form's module:

  Option Explicit

  Private Sub Command1_Click()
     'Enable the timer to begin printer status checks.
     Timer1.Enabled = True
     
     'Enable and disable start and stop buttons.
     Command1.Enabled = False
     Command2.Enabled = True
     Command3.Enabled = True
  End Sub

  Private Sub Command2_Click()
     'Disable timer to stop further printer checks.
     Timer1.Enabled = False
     
     'Enable and disable start and stop buttons.
     Command1.Enabled = True
     Command2.Enabled = False
     Command3.Enabled = True
  End Sub

  Private Sub Command3_Click()
     'Clear the status info.
     Text1.Text = ""
     Text2.Text = ""
     Text3.Text = ""
  End Sub

  Private Sub Form_Load()
     'Initialize captions for control buttons.
     Command1.Caption = "Start"
     Command2.Caption = "Stop"
     Command3.Caption = "Clear"
     
     'Clear the status info.
     Text1.Text = ""
     Text2.Text = ""
     Text3.Text = ""
     
     Command1.Enabled = True
     'Disable stop and clear buttons.
     Command2.Enabled = False
     Command3.Enabled = False
     
     'Set interval for printer status checking to 1/2 second.
     Timer1.Enabled = False
     Timer1.Interval = 500
  End Sub

  Private Sub Timer1_Timer()
     Dim PrinterStatus As String
     Dim JobStatus As String
     Dim ErrorInfo As String
     
     'Clear the status info for new info/status.
     Text1.Text = ""
     Text2.Text = ""
     Text3.Text = ""
     
     'Call sub to perform check.
     Text1.Text = CheckPrinter(PrinterStatus, JobStatus)
     Text2.Text = PrinterStatus
     Text3.Text = JobStatus
  End Sub

  Public Function CheckPrinter(PrinterStr As String, JobStr As String) As String
     Dim hPrinter As Long
     Dim ByteBuf As Long
     Dim BytesNeeded As Long
     Dim PI2 As PRINTER_INFO_2
     Dim JI2 As JOB_INFO_2
     Dim PrinterInfo() As Byte
     Dim JobInfo() As Byte
     Dim result As Long
     Dim LastError As Long
     Dim PrinterName As String
     Dim tempStr As String
     Dim NumJI2 As Long
     Dim pDefaults As PRINTER_DEFAULTS
     Dim I As Integer
     
     'Set a default return value if no errors occur.
     CheckPrinter = "Printer info retrieved"
     
     'NOTE: You can pick a printer from the Printers Collection
     'or use the EnumPrinters() API to select a printer name.
     
     'Use the default printer of Printers collection.
     'This is typically, but not always, the system default printer.
     PrinterName = Printer.DeviceName
     
     'Set desired access security setting.
     pDefaults.DesiredAccess = PRINTER_ACCESS_USE
     
     'Call API to get a handle to the printer.
     result = OpenPrinter(PrinterName, hPrinter, pDefaults)
     If result = 0 Then
        'If an error occurred, display an error and exit sub.
        CheckPrinter = "Cannot open printer " &amp; PrinterName &amp; _
           ", Error: " &amp; Err.LastDllError
        Exit Function
     End If

     'Init BytesNeeded
     BytesNeeded = 0

     'Clear the error object of any errors.
     Err.Clear

     'Determine the buffer size that is needed to get printer info.
     result = GetPrinter(hPrinter, 2, 0&amp;, 0&amp;, BytesNeeded)
     
     'Check for error calling GetPrinter.
     If Err.LastDllError &lt;&gt; ERROR_INSUFFICIENT_BUFFER Then
        'Display an error message, close printer, and exit sub.
        CheckPrinter = " &gt; GetPrinter Failed on initial call! &lt;"
        ClosePrinter hPrinter
        Exit Function
     End If
     
     'Note that in Charles Petzold's book "Programming Windows 95," he
     'states that because of a problem with GetPrinter on Windows 95 only, you 
     'must allocate a buffer as much as three times larger than the value 
     'returned by the initial call to GetPrinter. This is not done here.
     ReDim PrinterInfo(1 To BytesNeeded)
     
     ByteBuf = BytesNeeded
     
     'Call GetPrinter to get the status.
     result = GetPrinter(hPrinter, 2, PrinterInfo(1), ByteBuf, _
       BytesNeeded)
     
     'Check for errors.
     If result = 0 Then
        'Determine the error that occurred.
        LastError = Err.LastDllError()
        
        'Display error message, close printer, and exit sub.
        CheckPrinter = "Couldn't get Printer Status!  Error = " _
           &amp; LastError
        ClosePrinter hPrinter
        Exit Function
     End If

     'Copy contents of printer status byte array into a
     'PRINTER_INFO_2 structure to separate the individual elements.
     CopyMemory PI2, PrinterInfo(1), Len(PI2)
     
     'Check if printer is in ready state.
     PrinterStr = CheckPrinterStatus(PI2.Status)
     
     'Add printer name, driver, and port to list.
     PrinterStr = PrinterStr &amp; "Printer Name = " &amp; _
       GetString(PI2.pPrinterName) &amp; vbCrLf
     PrinterStr = PrinterStr &amp; "Printer Driver Name = " &amp; _
       GetString(PI2.pDriverName) &amp; vbCrLf
     PrinterStr = PrinterStr &amp; "Printer Port Name = " &amp; _
       GetString(PI2.pPortName) &amp; vbCrLf
     
     'Call API to get size of buffer that is needed.
     result = EnumJobs(hPrinter, 0&amp;, &amp;HFFFFFFFF, 2, ByVal 0&amp;, 0&amp;, _
        BytesNeeded, NumJI2)
     
     'Check if there are no current jobs, and then display appropriate message.
     If BytesNeeded = 0 Then
        JobStr = "No Print Jobs!"
     Else
        'Redim byte array to hold info about print job.
        ReDim JobInfo(0 To BytesNeeded)
        
        'Call API to get print job info.
        result = EnumJobs(hPrinter, 0&amp;, &amp;HFFFFFFFF, 2, JobInfo(0), _
          BytesNeeded, ByteBuf, NumJI2)
        
        'Check for errors.
        If result = 0 Then
           'Get and display error, close printer, and exit sub.
           LastError = Err.LastDllError
           CheckPrinter = " &gt; EnumJobs Failed on second call! &lt;  Error = " _
              &amp; LastError
           ClosePrinter hPrinter
           Exit Function
        End If
        
        'Copy contents of print job info byte array into a
        'JOB_INFO_2 structure to separate the individual elements.
        For I = 0 To NumJI2 - 1   ' Loop through jobs and walk the buffer
            CopyMemory JI2, JobInfo(I * Len(JI2)), Len(JI2)
               
            ' List info available on Jobs.
            Debug.Print "Job ID" &amp; vbTab &amp; JI2.JobId
            Debug.Print "Name Of Printer" &amp; vbTab &amp; _
              GetString(JI2.pPrinterName)
            Debug.Print "Name Of Machine That Created Job" &amp; vbTab &amp; _
              GetString(JI2.pMachineName)
            Debug.Print "Print Job Owner's Name" &amp; vbTab &amp; _
              GetString(JI2.pUserName)
            Debug.Print "Name Of Document" &amp; vbTab &amp; GetString(JI2.pDocument)
            Debug.Print "Name Of User To Notify" &amp; vbTab &amp; _
              GetString(JI2.pNotifyName)
            Debug.Print "Type Of Data" &amp; vbTab &amp; GetString(JI2.pDatatype)
            Debug.Print "Print Processor" &amp; vbTab &amp; _
              GetString(JI2.pPrintProcessor)
            Debug.Print "Print Processor Parameters" &amp; vbTab &amp; _
              GetString(JI2.pParameters)
            Debug.Print "Print Driver Name" &amp; vbTab &amp; _
              GetString(JI2.pDriverName)
            Debug.Print "Print Job 'P' Status" &amp; vbTab &amp; _
              GetString(JI2.pStatus)
            Debug.Print "Print Job Status" &amp; vbTab &amp; JI2.Status
            Debug.Print "Print Job Priority" &amp; vbTab &amp; JI2.Priority
            Debug.Print "Position in Queue" &amp; vbTab &amp; JI2.Position
            Debug.Print "Earliest Time Job Can Be Printed" &amp; vbTab &amp; _
              JI2.StartTime
            Debug.Print "Latest Time Job Will Be Printed" &amp; vbTab &amp; _
              JI2.UntilTime
            Debug.Print "Total Pages For Entire Job" &amp; vbTab &amp; JI2.TotalPages
            Debug.Print "Size of Job In Bytes" &amp; vbTab &amp; JI2.Size
            'Because of a bug in Windows NT 3.51, the time member is not set correctly.
            'Therefore, do not use the time member on Windows NT 3.51.
            Debug.Print "Elapsed Print Time" &amp; vbTab &amp; JI2.time
            Debug.Print "Pages Printed So Far" &amp; vbTab &amp; JI2.PagesPrinted
               
            'Display basic job status info.
            JobStr = JobStr &amp; "Job ID = " &amp; JI2.JobId &amp; _
               vbCrLf &amp; "Total Pages = " &amp; JI2.TotalPages &amp; vbCrLf
            
            tempStr = ""   'Clear
            'Check for a ready state.
            If JI2.pStatus = 0&amp; Then   ' If pStatus is Null, check Status.
              If JI2.Status = 0 Then
                 tempStr = tempStr &amp; "Ready!  " &amp; vbCrLf
              Else  'Check for the various print job states.
                 If (JI2.Status And JOB_STATUS_SPOOLING) Then
                    tempStr = tempStr &amp; "Spooling  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_OFFLINE) Then
                    tempStr = tempStr &amp; "Off line  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_PAUSED) Then
                    tempStr = tempStr &amp; "Paused  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_ERROR) Then
                    tempStr = tempStr &amp; "Error  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_PAPEROUT) Then
                    tempStr = tempStr &amp; "Paper Out  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_PRINTING) Then
                    tempStr = tempStr &amp; "Printing  "
                 End If
                 
                 If (JI2.Status And JOB_STATUS_USER_INTERVENTION) Then
                    tempStr = tempStr &amp; "User Intervention Needed  "
                 End If
                 
                 If Len(tempStr) = 0 Then
                    tempStr = "Unknown Status of " &amp; JI2.Status
                 End If
              End If
          Else
              ' Dereference pStatus.
              tempStr = PtrCtoVbString(JI2.pStatus)
          End If
            
            'Report the Job status.
            JobStr = JobStr &amp; tempStr &amp; vbCrLf
            Debug.Print JobStr &amp; tempStr
        Next I
     End If
     
     'Close the printer handle.
     ClosePrinter hPrinter
  End Function

7. Go to your print queue, and then pause the printer. Note that you may need a
  local printer instead of a network printer to pause the print queue.

8. Run the sample project, and then click Start.

9. Perform one or more print operations from another application such as
  Notepad. The printer information is displayed in the text boxes. Notice that
  job details appear in the Immediate window.

10. If necessary, click Stop on the form to scroll through the text boxes and
  the Immediate window to view this printer information.

Troubleshooting
---------------

- Only a specific device driver can obtain real-time, accurate printer status
  information. This code obtains the same status that the Windows Spooler
  reports.

- The exact status that is reported may vary with different printers and
  drivers.

REFERENCES
==========

For additional information about how printer status and print job status are
set, click the article number below to view the article in the Microsoft
Knowledge Base:

  Q160129 HOWTO: Get the Status of a Printer and a Print Job

For additional information about how to use the EnumPrinters API function, click
the article number below to view the article in the Microsoft Knowledge Base:

  Q166008 ACC: Enumerating Local and Network Printers

For additional information about how to retrieve print job information in
Microsoft Visual C++, click the article numbers below to view the articles in
the Microsoft Knowledge Base:

  Q228769 HOWTO: Retrieve Print Job Information

  Q158828 HOWTO: How To Call Win32 Spooler Enumeration APIs Properly

Additional query words: printer queue

======================================================================
Keywords          : kbAPI kbPrinting kbSpooler KbVBA kbVBp kbVBp400 kbVBp500 kbVBp600 kbGrpDSVB kbDSupport kbHOWTOmaster 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB500Search kbVB600Search kbVB500 kbVB600 kbVB400Search kbVB400
Version           : :4.0,5.0,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="http://github.com/jeffpar">jeffpar</a><br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>