<!DOCTYPE html>
<html lang="en-US">
<head>
    <meta charset='utf-8'>
    <meta http-equiv="X-UA-Compatible" content="chrome=1">
    <link href='https://fonts.googleapis.com/css?family=Chivo:900' rel='stylesheet' type='text/css'>
    <link rel="stylesheet" href="/kbarchive/assets/css/style.css?v=4c5dc89ca309d19d6d9171a268a55a16d7399494">
    <link rel="stylesheet" type="text/css" href="/kbarchive/assets/css/print.css" media="print">
    <!--[if lt IE 9]>
    <script src="//html5shiv.googlecode.com/svn/trunk/html5.js"></script>
    <![endif]-->

    <!-- Begin Jekyll SEO tag v2.3.0 -->
<title>Q147886: HOWTO: How VB Can Get Windows Status Information via API Calls | KnowledgeBase Archive</title>
<meta property="og:title" content="Q147886: HOWTO: How VB Can Get Windows Status Information via API Calls" />
<meta property="og:locale" content="en_US" />
<meta name="description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<meta property="og:description" content="An Archive of Early Microsoft KnowledgeBase Articles" />
<link rel="canonical" href="http://jeffpar.github.io/kbarchive/kb/147/Q147886/" />
<meta property="og:url" content="http://jeffpar.github.io/kbarchive/kb/147/Q147886/" />
<meta property="og:site_name" content="KnowledgeBase Archive" />
<script type="application/ld+json">
{"name":null,"description":"An Archive of Early Microsoft KnowledgeBase Articles","author":null,"@type":"WebPage","url":"http://jeffpar.github.io/kbarchive/kb/147/Q147886/","publisher":null,"image":null,"headline":"Q147886: HOWTO: How VB Can Get Windows Status Information via API Calls","dateModified":null,"datePublished":null,"sameAs":null,"mainEntityOfPage":null,"@context":"http://schema.org"}</script>
<!-- End Jekyll SEO tag -->

</head>

<body>
<div id="container">
    <div class="inner">

        <header>
            <h1><a href="/kbarchive">KnowledgeBase Archive</a></h1>
            <h2>An Archive of Early Microsoft KnowledgeBase Articles</h2>
        </header>
        <section id="downloads" class="clearfix">
            
            
            <a href="http://github.com/jeffpar/kbarchive" id="view-on-github" class="button"><span>View on GitHub</span></a>
            
        </section>
        <hr>
        <section id="main_content">
                <div>
        <h2 id="q147886-howto-how-vb-can-get-windows-status-information-via-api-calls">Q147886: HOWTO: How VB Can Get Windows Status Information via API Calls</h2>

<div class="highlighter-rouge"><div class="highlight"><pre class="highlight"><code>Article: Q147886
Product(s): Microsoft Visual Basic for Windows
Version(s): 4.0,5.0,6.0
Operating System(s): 
Keyword(s): kbAPI kbSDKWin32 kbVBp kbVBp400 kbVBp500 kbVBp600 kbGrpDSVB kbDSupport
Last Modified: 18-FEB-2002

-------------------------------------------------------------------------------
The information in this article applies to:

- Microsoft Visual Basic Learning Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Professional Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Enterprise Edition for Windows, versions 5.0, 6.0 
- Microsoft Visual Basic Standard Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Professional Edition, 16-bit, for Windows, version 4.0 
- Microsoft Visual Basic Professional Edition, 32-bit, for Windows, version 4.0 
- Microsoft Visual Basic Enterprise Edition, 16-bit, for Windows, version 4.0 
- Microsoft Visual Basic Enterprise Edition, 32-bit, for Windows, version 4.0 
-------------------------------------------------------------------------------

SUMMARY
=======

The Visual Basic for Windows sample program below demonstrates how you can
obtain system status information similar to the information displayed in the
Windows Program Manager About box. The sample program displays the following
information using the Windows API functions indicated:

- The Windows version number with the GetVersion function.

- The CPU processor type.

- Whether Windows is running in enhanced mode or standard mode on a 16-bit
  operating system. This does not apply to Microsoft Windows 95 or Microsoft
  Windows NT.

- The amount of free memory.

- The percentage of free system resources with SystemHeapInfo on a 16-bit
  operating system. This does not apply to Microsoft Windows 95 or Microsoft
  Windows NT.

NOTE: The SystemHeapInfo API function is new to Microsoft Windows version
     3.1 and is not available in Microsoft Windows, version 3.0. All other
     API functions listed above are available in both Windows versions 3.0
     or 3.1.

MORE INFORMATION
================

Steps to Create Example Program
-------------------------------

1. Start a new project in Visual Basic. Form1 is created by default.

2. Add a module to the project.

3. Add the following code to the General Declarations section of Module1:

     #If Win32 Then
         Type SYSTEM_INFO
                 dwOemID As Long
                 dwPageSize As Long
                 lpMinimumApplicationAddress As Long
                 lpMaximumApplicationAddress As Long
                 dwActiveProcessorMask As Long
                 dwNumberOrfProcessors As Long
                 dwProcessorType As Long
                 dwAllocationGranularity As Long
                 dwReserved As Long
         End Type
         Type OSVERSIONINFO
                 dwOSVersionInfoSize As Long
                 dwMajorVersion As Long
                 dwMinorVersion As Long
                 dwBuildNumber As Long
                 dwPlatformId As Long
                 szCSDVersion As String * 128
         End Type
         Type MEMORYSTATUS
                 dwLength As Long
                 dwMemoryLoad As Long
                 dwTotalPhys As Long
                 dwAvailPhys As Long
                 dwTotalPageFile As Long
                 dwAvailPageFile As Long
                 dwTotalVirtual As Long
                 dwAvailVirtual As Long
         End Type

         Declare Function GetVersionEx Lib "kernel32" Alias "GetVersionExA" _
            (LpVersionInformation As OSVERSIONINFO) As Long
         Declare Sub GlobalMemoryStatus Lib "kernel32" (lpBuffer As _
            MEMORYSTATUS)
         Declare Sub GetSystemInfo Lib "kernel32" (lpSystemInfo As _
            SYSTEM_INFO)

         Public Const PROCESSOR_INTEL_386 = 386
         Public Const PROCESSOR_INTEL_486 = 486
         Public Const PROCESSOR_INTEL_PENTIUM = 586
         Public Const PROCESSOR_MIPS_R4000 = 4000
         Public Const PROCESSOR_ALPHA_21064 = 21064
     #Else
        ' Constants for GetWinFlags.
        Global Const WF_CPU286 = &amp;H2
        Global Const WF_CPU386 = &amp;H4
        Global Const WF_CPU486 = &amp;H8
        Global Const WF_80x87 = &amp;H400
        Global Const WF_STANDARD = &amp;H10
        Global Const WF_ENHANCED = &amp;H20
        Global Const WF_WINNT = &amp;H4000

        ' Type for SystemHeapInfo.
        Type SYSHEAPINFO
           dwSize As Long
           wUserFreePercent As Integer
           wGDIFreePercent As Integer
           hUserSegment As Integer
           hGDISegment As Integer
        End Type

        Declare Function GetVersion Lib "kernel" () As Long
        Declare Function GetWinFlags Lib "kernel" () As Long
        'Enter each of the following Declare statements as one, single line:
        Declare Function GetFreeSpace Lib "kernel" (ByVal wFlags As Integer)_
           As Long
        Declare Function GlobalCompact Lib "kernel" _
           (ByVal dwMinFree As Long) As Long
        Declare Function SystemHeapInfo Lib "toolhelp.dll" _
           (shi As SYSHEAPINFO) As Integer
     #End If

4. Enter the following code into the Form_Load procedure of Form1:

     Private Sub Form_Load()
        Dim msg As String         ' Status information.
        Dim nl As String          ' New-line.
        nl = Chr$(13) + Chr$(10)  ' New-line.

        Show
        MousePointer = 11   ' Hourglass.

        #If Win32 Then
            ' Get operating system and version.
            Dim verinfo As OSVERSIONINFO
            verinfo.dwOSVersionInfoSize = Len(verinfo)
            ret% = GetVersionEx(verinfo)
            If ret% = 0 Then
                MsgBox "Error Getting Version Information"
                End
            End If
            Select Case verinfo.dwPlatformId
                Case 0
                    msg = msg + "Windows 32s "
                Case 1
                    msg = msg + "Windows 95 "
                Case 2
                    msg = msg + "Windows NT "
            End Select

            ver_major$ = verinfo.dwMajorVersion
            ver_minor$ = verinfo.dwMinorVersion
            build$ = verinfo.dwBuildNumber
            msg = msg + ver_major$ + "." + ver_minor$
            msg = msg + " (Build " + build$ + ")" + nl + nl

            ' Get CPU type and operating mode.
            Dim sysinfo As SYSTEM_INFO
            GetSystemInfo sysinfo
            msg = msg + "CPU: "
            Select Case sysinfo.dwProcessorType
                Case PROCESSOR_INTEL_386
                    msg = msg + "Intel 386" + nl
                Case PROCESSOR_INTEL_486
                    msg = msg + "Intel 486" + nl
                Case PROCESSOR_INTEL_PENTIUM
                    msg = msg + "Intel Pentium" + nl
                Case PROCESSOR_MIPS_R4000
                    msg = msg + "MIPS R4000" + nl
                Case PROCESSOR_ALPHA_21064
                    msg = msg + "DEC Alpha 21064" + nl
                Case Else
                    msg = msg + "(unknown)" + nl

            End Select
            msg = msg + nl
            ' Get free memory.
            Dim memsts As MEMORYSTATUS
            Dim memory&amp;
            GlobalMemoryStatus memsts
            memory&amp; = memsts.dwTotalPhys
            msg = msg + "Total Physical Memory: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl
            memory&amp; = memsts.dwAvailPhys
            msg = msg + "Available Physical Memory: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl
            memory&amp; = memsts.dwTotalVirtual
            msg = msg + "Total Virtual Memory: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl
            memory&amp; = memsts.dwAvailVirtual
            msg = msg + "Available Virtual Memory: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl _
                 + nl

            ' Get free system resources.
            ' Not applicable to 32-bit operating system (Windows NT).
        #Else
            ' Get operating system and version.
            ver&amp; = GetVersion()
            status&amp; = GetWinFlags()

            If status&amp; And WF_WINNT Then
                msg = msg + "Microsoft Windows NT "
            Else
                msg = msg + "Microsoft Windows "
            End If
            ver_major$ = Format$(ver&amp; And &amp;HFF)
            ver_minor$ = Format$((ver&amp; And &amp;HFF00) \ &amp;H100, "00")
            msg = msg + ver_major$ + "." + ver_minor$ + nl

            ' Get CPU type and operating mode.
            msg = msg + "CPU: "
            If status&amp; And WF_CPU286 Then msg = msg + "80286"
            If status&amp; And WF_CPU386 Then msg = msg + "80386"
            If status&amp; And WF_CPU486 Then msg = msg + "80486"
            If status&amp; And WF_80x87 Then msg = msg + " with 80x87"
            msg = msg + nl
            msg = msg + "Mode: "
            If status&amp; And WF_STANDARD Then msg = msg + "Standard" + nl
            If status&amp; And WF_ENHANCED Then msg = msg + "Enhanced" + nl

            ' Get free memory.
            memory&amp; = GetFreeSpace(0)
            msg = msg + "Memory free: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl
            memory&amp; = GlobalCompact(&amp;HFFFFFFFF)
            msg = msg + "Largest free block: "
            msg = msg + Format$(memory&amp; \ 1024, "###,###,###") + "K" + nl

            ' Get free system resources.
            ' The SystemHeapInfo API function became available in Windows
            '  version 3.1.
            msg = msg + "System resources: "
            If ver&amp; &gt;= &amp;H310 Then
                Dim shi As SYSHEAPINFO
                shi.dwSize = Len(shi)
                If SystemHeapInfo(shi) Then
                    If shi.wUserFreePercent &lt; shi.wGDIFreePercent Then
                        msg = msg + Format$(shi.wUserFreePercent) + "%"
                    Else
                        msg = msg + Format$(shi.wGDIFreePercent) + "%"
                    End If
                End If
            Else
                msg = msg + "n/a"
            End If
        #End If
        MsgBox msg, 0, "About " + Caption
        MousePointer = 0
     End Sub

5. Press the F5 key to run the program.

Additional query words:

======================================================================
Keywords          : kbAPI kbSDKWin32 kbVBp kbVBp400 kbVBp500 kbVBp600 kbGrpDSVB kbDSupport 
Technology        : kbVBSearch kbAudDeveloper kbZNotKeyword6 kbZNotKeyword2 kbVB500Search kbVB600Search kbVB500 kbVB600 kbVB400Search kbVB400 kbVB16bitSearch
Version           : :4.0,5.0,6.0
Issue type        : kbhowto

=============================================================================
</code></pre></div></div>


        <p>
            THE INFORMATION PROVIDED IN THE MICROSOFT KNOWLEDGE BASE IS
            PROVIDED "AS IS" WITHOUT WARRANTY OF ANY KIND.  MICROSOFT DISCLAIMS
            ALL WARRANTIES, EITHER EXPRESS OR IMPLIED, INCLUDING THE WARRANTIES
            OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE.  IN NO
            EVENT SHALL MICROSOFT CORPORATION OR ITS SUPPLIERS BE LIABLE FOR
            ANY DAMAGES WHATSOEVER INCLUDING DIRECT, INDIRECT, INCIDENTAL,
            CONSEQUENTIAL, LOSS OF BUSINESS PROFITS OR SPECIAL DAMAGES, EVEN IF
            MICROSOFT CORPORATION OR ITS SUPPLIERS HAVE BEEN ADVISED OF THE
            POSSIBILITY OF SUCH DAMAGES.  SOME STATES DO NOT ALLOW THE EXCLUSION
            OR LIMITATION OF LIABILITY FOR CONSEQUENTIAL OR INCIDENTAL DAMAGES
            SO THE FOREGOING LIMITATION MAY NOT APPLY.
        </p>
        <p>Copyright Microsoft Corporation 1986-2002.</p>
    </div>

        </section>

        <footer>
            
            KnowledgeBase Archive is maintained by <a href="http://github.com/jeffpar">jeffpar</a><br>
            
            This page was generated by <a href="https://pages.github.com">GitHub Pages</a>.
        </footer>

    </div>
</div>


</body>
</html>